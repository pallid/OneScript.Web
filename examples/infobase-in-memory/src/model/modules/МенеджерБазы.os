#Использовать json

Перем мПутьКФайлуНастроек;

Процедура Инициализация() Экспорт

	ИнформационнаяБаза.ВыполнитьКоманду("Create table goods (id INTEGER PRIMARY KEY, name TEXT, article TEXT, price double, description TEXT)");

	Парсер = Новый ПарсерJSON;
	Данные = Парсер.ПрочитатьJSON(ПрочитатьИсходныеДанные(),,,Истина);
	Если ТипЗнч(Данные) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;

	Для каждого Объект Из Данные Цикл
		ДобавитьНоменклатуру(Объект);
	КонецЦикла;

КонецПроцедуры

Функция ПрочитатьИсходныеДанные()
	
	Файл = Новый Файл(мПутьКФайлуНастроек);
	Если Не Файл.Существует() Тогда
		Содержимое = "[]";
	Иначе
		Текст = Новый ЧтениеТекста(мПутьКФайлуНастроек);
		Содержимое = Текст.Прочитать();
		Текст.Закрыть();
	КонецЕсли;
	
	Возврат Содержимое;
	
КонецФункции

Функция ДобавитьНоменклатуру(Данные) Экспорт

	Запрос = ИнформационнаяБаза.СоздатьЗапрос();
	Запрос.Текст = "insert into goods (name, article, price, description) values(@name, @article, @price, @description)";
	Запрос.УстановитьПараметр("name", 			Данные["name"]);
	Запрос.УстановитьПараметр("article", 		Данные["article"]);

	Цена = Данные["price"];
	Если Цена = Неопределено ИЛИ Цена = "" Тогда
		Цена = "0.00";
	КонецЕсли;	
	Запрос.УстановитьПараметр("price", 			Цена);

	Запрос.УстановитьПараметр("description", 	Данные["description"]);
	Запрос.ВыполнитьКоманду();

КонецФункции

Функция ПолучитьЭлементНоменклатуры(Знач Идентификатор) Экспорт

	Запрос = ИнформационнаяБаза.СоздатьЗапрос();
	Запрос.Текст = "select * from goods where id = @id";
	Запрос.УстановитьПараметр("id", Идентификатор);
	ТЗ = Запрос.Выполнить().Выгрузить();

	Если ТЗ.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат ТЗ[0];

КонецФункции

Функция УдалитьЭлементНоменклатуры(Знач Идентификатор) Экспорт

	Запрос = ИнформационнаяБаза.СоздатьЗапрос();
	Запрос.Текст = "delete from goods where id = @id";
	Запрос.УстановитьПараметр("id", Идентификатор);
	Запрос.ВыполнитьКоманду();

КонецФункции

Функция ИзменитьЭлементНоменклатуры(Знач Данные, Знач Идентификатор) Экспорт

	Запрос = ИнформационнаяБаза.СоздатьЗапрос();
	Запрос.Текст = "update goods  set name = @name, article = @article, price = @price, description = @description where id = @id";
	Запрос.УстановитьПараметр("id", 			Идентификатор);
	Запрос.УстановитьПараметр("name", 			Данные["name"]);
	Запрос.УстановитьПараметр("article", 		Данные["article"]);
	
	Цена = Данные["price"];
	Если Цена = Неопределено ИЛИ Цена = "" Тогда
		Цена = "0.00";
	КонецЕсли;

	Запрос.УстановитьПараметр("price", 			Цена);
	Запрос.УстановитьПараметр("description", 	Данные["description"]);
	Запрос.ВыполнитьКоманду();

КонецФункции

Функция ПолучитьСписокНоменклатуры() Экспорт

	Запрос = ИнформационнаяБаза.СоздатьЗапрос();
	Запрос.Текст = "select * from goods";
	ТЗ = Запрос.Выполнить().Выгрузить();

	Если ТЗ.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат ТЗ;

КонецФункции

Процедура ЗаписатьДанныеВФайл() Экспорт

	Текст = Новый ЗаписьТекста(мПутьКФайлуНастроек);
	Запись = Новый ПарсерJSON;
	ТЗ = ПолучитьСписокНоменклатуры();
	Текст.Записать(Запись.ЗаписатьJSON(ТЗ));
	Текст.Закрыть();

КонецПроцедуры

///////////////////////////////////////////////////
мПутьКФайлуНастроек = "appData.json";